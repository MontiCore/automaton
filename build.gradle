/* (c) https://github.com/MontiCore/monticore */
import de.monticore.MCTask

plugins {
    id "java"
    id "monticore"  version "6.2.0-SNAPSHOT" // MontiCore Plugin
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "4.0.4"
    // useful reports
    // todo: reactivate this plugin when new version compatible to gradle 6.0.1 is available
//    id 'cz.malohlava.visteg' version '1.0.5'
    id 'jacoco'
}


def mc_version  = '6.2.0-SNAPSHOT'
def grammar_version = '6.2.0-SNAPSHOT'
def guava_version = '25.1-jre'
def findbugs_version = '3.0.0'
def antlr_version = '4.7.1'
def junit_version = '4.11'
def logback_version = '1.1.2'

def grammarDir = 'src/main/grammars'

group = "de.monticore.lang"
version = '1.0.0-SNAPSHOT'
description = "Automata DSL"
sourceCompatibility = "1.8"

dependencies {
    grammar (group:'de.monticore', name:'monticore-grammar', version:grammar_version, classifier: "grammars",
            changing: true) { force = true }
    implementation group:'com.google.guava', name:'guava', version:guava_version
    implementation group:'com.google.code.findbugs', name:'jsr305', version:findbugs_version
    implementation group:'org.antlr', name:'antlr4-runtime', version:antlr_version
    implementation group:'de.monticore', name:'monticore-runtime', version:grammar_version
    implementation (group:'de.monticore', name:'monticore-generator', version:mc_version,
                   changing: true) { force = true }
    implementation (group:'de.monticore', name:'monticore-grammar', version:grammar_version,
                   changing: true) { force = true }
    testImplementation group:'junit', name:'junit', version:junit_version
    testImplementation (group:'de.monticore', name:'monticore-runtime', version:grammar_version,
                       classifier:"tests", changing: true) { force = true }
    testImplementation group:'ch.qos.logback', name:'logback-classic', version:logback_version
}

// has to be placed directly under the dependency definition, since otherwise the grammar configurations are not found
repositories {
    maven {
        credentials.username mavenUser
        credentials.password mavenPassword
        url repo
    }
    mavenLocal()
}

// configure non-standard source sets
sourceSets {
    main.java.srcDirs += [ "$projectDir/target/generated-sources/monticore/sourcecode"]
}
buildDir = file("$projectDir/target")

// one task per file
//fileTree(grammarDir).each {
//    def g = it
//    task  "generate${it.getName().substring(0,it.getName().lastIndexOf('.'))}" (type: MCTask) {
//        grammar = file g
//        outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
//        handcodedPath file("$projectDir/src/main/java")
//        modelPath file("$grammarDir")
//        def incCheckDir = file(grammarDir).toURI().relativize(g.toURI()).toString()
//        incCheckDir = incCheckDir.substring(0,incCheckDir.lastIndexOf('.')).toLowerCase()
//        def uptoDate = incCheck(outputDir.toString() +"/$incCheckDir/IncGenGradleCheck.txt")
//        outputs.upToDateWhen { uptoDate }
//    }
//}

task  generateAutomata (type: MCTask) {
  grammar = file "$grammarDir/Automata.mc4"
  outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
  def uptoDate = incCheck("Automata.mc4")
  outputs.upToDateWhen { uptoDate }
}

compileJava {
    dependsOn project.collect { it.tasks.withType(MCTask) }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from(sourceSets.main.allJava)
}

// build javadoc jar in addition
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier =  "javadoc"
}
// generated java doc contains errors, disable for now
javadoc.failOnError(false)

shadowJar { // all in one jar
    manifest {
        attributes "Main-Class": "automata.AutomataTool"
    }
    archiveClassifier =  "cli"
}

// build grammar jar as well
task grammarJar(type: Jar) {
    from ("$projectDir/$grammarDir"){ include "**/*.mc4" }
    archiveClassifier = "grammars"
}



// configure deployment
publishing {
    // configure what artifacts to publish
    publications {
        mavenJava(MavenPublication) {
            artifactId = "$project.name"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar
            artifact grammarJar
        }
    }
    repositories.maven {
        credentials.username mavenUser
        credentials.password mavenPassword
        def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
        def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
        url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// todo: reactivate this plugin when new version compatible to gradle 6.0.1 is available
//visteg {
//    destination    = 'target/reports/visteg.dot'
//}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the 'test' task
    reportOn tasks.withType(Test)
}
